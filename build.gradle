plugins {
    id "io.freefair.lombok" version "8.12.2.1"
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

sourceCompatibility = 17

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    springBootVersion = '3.4.5'
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    api "org.springframework:spring-web"
    api "org.springframework.boot:spring-boot-starter-test"
    implementation 'junit:junit'
    api "com.github.ahunigel:test-toolkit:1.1.3"

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'net.bytebuddy:byte-buddy' // allows mocking of classes (in addition to interfaces)
    testImplementation "org.objenesis:objenesis:3.4"

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
}
// Make all tests use JUnit 5
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = project.name
                description = 'spring test toolkit'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
